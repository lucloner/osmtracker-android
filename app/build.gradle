apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {

    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 30
    buildToolsVersion = '30.0.3'

    defaultConfig {
        applicationId "net.vicp.biggee.android.osmtracker"
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true

        testApplicationId "net.vicp.biggee.android.osmtracker.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        // Create a file $HOME/.gradle/gradle.properties
        // containing the values signing.storeFile=..., etc.
        if (project.hasProperty('signing.storeFile')) {
            release {
                storeFile file(project.property('signing.storeFile'))
                storePassword project.property('signing.storePassword')
                keyAlias project.property('signing.keyAlias')
                keyPassword project.property('signing.keyPassword')
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }


        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    android {
        lintOptions {
            abortOnError false
        }
    }

    defaultConfig {
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        animationsDisabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '22.0.7026061'

}

dependencies {

    implementation 'org.apache.james:apache-mime4j-core:0.8.3'

    implementation 'org.apache.httpcomponents:httpmime:4.5.13'
    //implementation 'org.apache.httpcomponents:httpclient-android:4.3.5'

    implementation 'org.osmdroid:osmdroid-android:6.1.1'
    implementation 'org.apache.httpcomponents:httpcore:4.4.14'

    implementation 'oauth.signpost:signpost-commonshttp4:2.1.1'
    implementation 'org.slf4j:slf4j-android:1.7.30'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'org.apache.commons:commons-io:1.3.2'


    // Required for local unit tests (JUnit 4 framework)
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.8.0'

    testImplementation 'org.powermock:powermock-core:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'

    // Required for instrumented tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    
    compileOnly 'org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2'

    // AndroidX Capable version
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
    implementation 'com.google.android.material:material:1.3.0'


}

task copyNorwegianValues(type: Copy) {
    from 'src/main/res/values-nb/'
    into 'src/main/res/values-no/'
}

task copyNorwegianXml(type: Copy) {
    from 'src/main/res/xml-nb/'
    into 'src/main/res/xml-no/'
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        // Copying 'nb' resources to 'no' (See pull request #6)
        output.processResources.dependsOn([copyNorwegianValues, copyNorwegianXml])
    }
}
repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
